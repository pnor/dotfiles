#+TITLE: Org Mode Configuration
#+AUTHOR: Phillip O'Reggio
#+PROPERTY: header-args :emacs-lisp :tangle yes :comments link
#+STARTUP: hideblocks overview
#+BEGIN_SRC emacs-lisp
;;; $DOOMDIR/config.el -*- lexical-binding: t; -*-
#+END_SRC

* General
** Private Information
Load some personal information
#+BEGIN_SRC emacs-lisp
(load! "Secret.el")
#+END_SRC

** Library Functions
Functions used in other places in the configs
#+BEGIN_SRC emacs-lisp
;; Get a live xwidget buffer with url opened
(defun personal--xwidget-buffer (url)
  (xwidget-webkit-browse-url url)
  (let ((xwidget-buf (seq-find
                      (lambda (b) (eq (buffer-local-value 'major-mode b) 'xwidget-webkit-mode))
                      (buffer-list))))
    xwidget-buf))
#+END_SRC
** Font, Themes, and the General Look and Behavior
*** Look
Set the color theme and font
#+BEGIN_SRC emacs-lisp
;; (setq doom-theme 'starry)
(setq doom-theme 'starry-transparent)
;; (setq doom-font (font-spec :family "MesloLGS Nerd Font" :size 16))
(setq doom-font (font-spec :family "CozetteVector" :size 18))

;; (setq doom-font (font-spec :family "Cascadia Code" :size 18))
#+END_SRC

Cursor color
#+begin_src emacs-lisp
(setq evil-normal-state-cursor '(box "#528AF2")
      evil-insert-state-cursor '(bar "#528AF2")
      evil-visual-state-cursor '(hollow "#FDDC87")
      evil-operator-state-cursor '(evil-half-cursor "#528AF2")
      evil-replace-state-cursor '(hbar "#FDDC87"))
#+end_src

Make the emacs frame transparent because I like my laptop background ðŸ™ƒ
#+BEGIN_SRC emacs-lisp
(defun personal-set-alpha-background-frame-params ()
  (let ((bg-transparency 0.3))
    (set-frame-parameter (selected-frame) 'alpha-background bg-transparency)
    (add-to-list 'default-frame-alist `(alpha-background . ,bg-transparency))))

(add-hook 'doom-load-theme-hook #'personal-set-alpha-background-frame-params)
#+END_SRC

And some functions to manipulate transparency on the frame
#+begin_src emacs-lisp
(defun personal-set-alpha-background-on-frame ()
  (interactive)
  (let ((alpha (read-number "Enter transparency (0.0-1.0): ")))
    (set-frame-parameter (selected-frame) 'alpha-background alpha)))
#+end_src

+Make emacs start full screen+
Emacs starts in a small frame, and goes full screen when reloading session
#+begin_src emacs-lisp
(if (display-graphic-p)
    (defadvice! personal-full-screen-on-reload-last ()
      :after #'doom/quickload-session
      (toggle-frame-fullscreen)))
(setq initial-frame-alist '((top . 100) (left . 400) (width . 100) (height . 40)))

#+end_src

Show global display fill column
#+BEGIN_SRC emacs-lisp
(global-display-fill-column-indicator-mode t)
#+END_SRC

Show Line numbers
#+BEGIN_SRC emacs-lisp
(setq display-line-numbers-type t)
#+END_SRC

Modify the appearance of the mode-line
#+BEGIN_SRC elisp
(setq doom-modeline-major-mode-icon t)
#+END_SRC

Make some faces use variable pitch
#+begin_src emacs-lisp
(custom-theme-set-faces
   'user
   '(line-number ((t (:inherit fixed-pitch))))
   '(line-number-current-line ((t (:inherit fixed-pitch))))
   '(font-lock-comment-face ((t (:inherit fixed-pitch))))
   ;; '(doom-modeline-buffer-path ((t (:inherit fixed-pitch))))
   ;; '(doom-modeline-buffer-file ((t (:inherit fixed-pitch))))
   ;; '(doom-modeline-buffer-modified ((t (:inherit fixed-pitch))))
   ;; '(doom-modeline-buffer-host ((t (:inherit fixed-pitch))))
   '(doom-modeline-project-dir ((t (:inherit fixed-pitch))))
   '(doom-modeline-project-root-dir ((t (:inherit fixed-pitch))))
   '(doom-modeline-project-parent-dir ((t (:inherit fixed-pitch))))
   '(doom-modeline-buffer-major-mode ((t (:inherit fixed-pitch)))))
#+end_src

And change the font used for variable pitch fonts
#+begin_src emacs-lisp
(defun personal--set-variable-pitch-faces ()
  (interactive)
  (custom-theme-set-faces
   'user
   '(variable-pitch ((t (:family "Helvetica" :height 200 :weight normal))))
   '(fixed-pitch ((t ( :family "MesloLGS Nerd Font" :height 160))))))

(personal--set-variable-pitch-faces)

#+end_src

*** Behavior
Add a fill column to prevent long lines (116 is the width of the vertical monitor)
#+BEGIN_SRC emacs-lisp
(setq-default fill-column 100)
#+END_SRC

and automatically break long lines in source code buffers and org mode files
#+BEGIN_SRC emacs-lisp
(add-hook! prog-mode (auto-fill-mode t))
(add-hook! org-mode (auto-fill-mode t))
#+END_SRC

Show a message when doing garbage collection
#+BEGIN_SRC emacs-lisp
(setq garbage-collection-messages t)
#+END_SRC

Have evil-mode treat emacs /symbols/ as words instead of by character
(symbol definition changes based on the language)
#+BEGIN_SRC emacs-lisp
(with-eval-after-load 'evil
    (defalias #'forward-evil-word #'forward-evil-symbol)
    ;; make evil-search-word look for symbol rather than word boundaries
    (setq-default evil-symbol-word-search t))

#+END_SRC

** Splash Screen Edits
Remove the options on the splash screen I don't use
#+BEGIN_SRC emacs-lisp
(defun personal-change-doom-dashboard-options ()
  "Change dashboard options for start screen"
  (setq +doom-dashboard-menu-sections
        '(("Reload last session" :icon
           (all-the-icons-octicon "history" :face 'doom-dashboard-menu-title)
           :when
           (cond
            ((featurep! :ui workspaces)
             (file-exists-p
              (expand-file-name persp-auto-save-fname persp-save-dir)))
            ((require 'desktop nil t)
             (file-exists-p
              (desktop-full-file-name))))
           :face
           (:inherit
            (doom-dashboard-menu-title bold))
           :action doom/quickload-session)
          ("Open private configuration" :icon
           (all-the-icons-octicon "tools" :face 'doom-dashboard-menu-title)
           :when
           (file-directory-p doom-private-dir)
           :action doom/open-private-config))))

(personal-change-doom-dashboard-options)
#+END_SRC

Last crash info
#+begin_src emacs-lisp
(defun personal--last-crash-delta (crash-log-dir emacs-crash-cache-file)
  "Computes time since last crash.
Return result as a numeric number that needs to be converted to human readable string using `ts-human-format-duration'.

CRASH-LOG-DIR is the directory where Emacs crash log is located (used to run touch).

EMACS-CRASH-CACHE-file is a file (may not exist yet) that stores the time of the latest crash. Is
used when the CRASH-LOG-DIR has no information for the last crash time, and is created/updated on
each parse."
  (require 'ts)
  (let* ((dir-name (expand-file-name crash-log-dir))
         (last-crash-log (shell-command-to-string (format "ls -r %s | rg -i emacs | head -1"
                                                          dir-name))))
    (if (length= last-crash-log 0)
        (personal--use-crash-cache (expand-file-name emacs-crash-cache-file))
      (personal--use-crash-log
       (expand-file-name crash-log-dir)
       (expand-file-name emacs-crash-cache-file)))))

(defun personal--use-crash-cache (emacs-crash-cache-file)
  (unless (file-exists-p emacs-crash-cache-file)
    (write-region (shell-command-to-string "date -R") nil emacs-crash-cache-file))
  (let ((last-crash-string (with-temp-buffer
                        (insert-file-contents emacs-crash-cache-file)
                        (buffer-string))))
    (personal--compute-delta last-crash-string)))

(defun personal--use-crash-log (log-file cache-file)
  (let ((last-crash-string
         (shell-command-to-string (format "date -r %s -R" log-file))))
    (write-region last-crash-string nil cache-file)
    (personal--compute-delta last-crash-string)))

(defun personal--compute-delta (last-crash-time-string)
  (ts-diff (ts-parse (shell-command-to-string "date -R")) (ts-parse last-crash-time-string)))

(defun personal--is-longest-no-crash-time (delta best-time-file)
  "Return `t' if DELTA is bigger than the number in BEST-TIME-FILE.
Also handles updating the number if it is bigger, or creating it if it doesn't exist."
  (let ((best-time-file-name (expand-file-name best-time-file)))
    (unless (file-exists-p best-time-file-name)
      (write-region "0" nil best-time-file-name))
    (let ((best-delta (string-to-number (with-temp-buffer
                        (insert-file-contents best-time-file-name)
                        (buffer-string)))))
      (if (> delta best-delta)
          (progn
            ;; write that to file instead
            (write-region (number-to-string delta) nil best-time-file-name)
            ;; return t
            t)
        nil))))


(defun doom-last-crash-line ()
  "Say how long since Emacs last crashed.
BEST-TIME-NAME is optional and specifies absolute path to file that contains the longest time Emacs"
  (let* ((delta (personal--last-crash-delta "~/Library/Logs/DiagnosticReports" "~/.doom.d/splash-last-crash.txt"))
         (delta-string (ts-human-format-duration delta)))
    (if (personal--is-longest-no-crash-time delta "~/.doom.d/splash-longest-last-crash.txt")
        (format "%s since last incident! (NEW RECORD!)" delta-string)
      (format "%s since last incident!" delta-string))))

(defun doom-dashboard-phrase ()
  "Get a splash phrase, flow it over multiple lines as needed, and make fontify it."
  (mapconcat
   (lambda (line)
     (+doom-dashboard--center
      +doom-dashboard--width
      (with-temp-buffer
        (insert-text-button
         line
         'action
         (lambda (_) (+doom-dashboard-reload t))
         'face 'doom-dashboard-menu-title
         'mouse-face 'doom-dashboard-menu-title
         'help-echo "Last crash"
         'follow-link t)
        (buffer-string))))
   (split-string
    (with-temp-buffer
      (insert (doom-last-crash-line))
      (setq fill-column (min 70 (/ (* 2 (window-width)) 3)))
      (fill-region (point-min) (point-max))
      (buffer-string))
    "\n")
   "\n"))

;; Only place this message if ts is available (avoid startup errors)
(when (require 'ts nil 'noerror)
  (defadvice! doom-dashboard-widget-loaded-with-phrase ()
    :override #'doom-dashboard-widget-loaded
    (setq line-spacing 0.2)
    (insert
     "\n\n"
     (propertize
      (+doom-dashboard--center
       +doom-dashboard--width
       (doom-display-benchmark-h 'return))
      'face 'doom-dashboard-loaded)
     "\n"
     (doom-dashboard-phrase)
     "\n")))

(add-hook 'doom-load-theme-hook #'personal-change-doom-dashboard-options)
#+end_src

Changing image of emacs splash (copied largely from [[https://github.com/tecosaur/emacs-config/blob/master/config.org#splash-screen][here]]):
#+begin_src emacs-lisp
(defvar fancy-splash-image-template
  (expand-file-name "splash-images/emacs-e-template.svg" doom-private-dir)
  "Default template svg used for the splash image, with substitutions from ")

(defvar fancy-splash-sizes
  `((:height 300 :min-height 50 :padding (0 . 2))
    (:height 250 :min-height 42 :padding (2 . 4))
    (:height 200 :min-height 35 :padding (3 . 3))
    (:height 150 :min-height 28 :padding (3 . 3))
    (:height 100 :min-height 20 :padding (2 . 2))
    (:height 75  :min-height 15 :padding (2 . 1))
    (:height 50  :min-height 10 :padding (1 . 0))
    (:height 1   :min-height 0  :padding (0 . 0)))
  "list of plists with the following properties
  :height the height of the image
  :min-height minimum `frame-height' for image
  :padding `+doom-dashboard-banner-padding' (top . bottom) to apply
  :template non-default template file
  :file to use instead of template")

(defvar fancy-splash-template-colours
  '(("$colour1" . keywords) ("$colour2" . type) ("$colour3" . base5) ("$colour4" . base8))
  "list of colour-replacement alists of the form (\"$placeholder\" . 'theme-colour) which applied the template")

(unless (file-exists-p (expand-file-name "theme-splashes" doom-cache-dir))
  (make-directory (expand-file-name "theme-splashes" doom-cache-dir) t))

(defun fancy-splash-filename (theme-name height)
  (expand-file-name (concat (file-name-as-directory "theme-splashes")
                            theme-name
                            "-" (number-to-string height) ".svg")
                    doom-cache-dir))

(defun fancy-splash-clear-cache ()
  "Delete all cached fancy splash images"
  (interactive)
  (delete-directory (expand-file-name "theme-splashes" doom-cache-dir) t)
  (message "Cache cleared!"))

(defun fancy-splash-generate-image (template height)
  "Read TEMPLATE and create an image if HEIGHT with colour substitutions as
   described by `fancy-splash-template-colours' for the current theme"
  (with-temp-buffer
    (insert-file-contents template)
    (re-search-forward "$height" nil t)
    (replace-match (number-to-string height) nil nil)
    (dolist (substitution fancy-splash-template-colours)
      (goto-char (point-min))
      (while (re-search-forward (car substitution) nil t)
        ;; (replace-match (face-attribute 'foreground (cdr substitution)))
        (replace-match "#cccccc")
        ))
    (write-region nil nil
                  (fancy-splash-filename (symbol-name doom-theme) height) nil nil)))

(doom-color 'hl-line)

(defun fancy-splash-generate-images ()
  "Perform `fancy-splash-generate-image' in bulk"
  (dolist (size fancy-splash-sizes)
    (unless (plist-get size :file)
      (fancy-splash-generate-image (or (plist-get size :template)
                                       fancy-splash-image-template)
                                   (plist-get size :height)))))

(defun ensure-theme-splash-images-exist (&optional height)
  (unless (file-exists-p (fancy-splash-filename
                          (symbol-name doom-theme)
                          (or height
                              (plist-get (car fancy-splash-sizes) :height))))
    (fancy-splash-generate-images)))

(defun get-appropriate-splash ()
  (let ((height (frame-height)))
    (cl-some (lambda (size) (when (>= height (plist-get size :min-height)) size))
             fancy-splash-sizes)))

(setq fancy-splash-last-size nil)
(setq fancy-splash-last-theme nil)
(defun set-appropriate-splash (&rest _)
  (let ((appropriate-image (get-appropriate-splash)))
    (unless (and (equal appropriate-image fancy-splash-last-size)
                 (equal doom-theme fancy-splash-last-theme)))
    (unless (plist-get appropriate-image :file)
      (ensure-theme-splash-images-exist (plist-get appropriate-image :height)))
    (setq fancy-splash-image
          (or (plist-get appropriate-image :file)
              (fancy-splash-filename (symbol-name doom-theme) (plist-get appropriate-image :height))))
    (setq +doom-dashboard-banner-padding (plist-get appropriate-image :padding))
    (setq fancy-splash-last-size appropriate-image)
    (setq fancy-splash-last-theme doom-theme)
    (+doom-dashboard-reload)))

(when (display-graphic-p)
  ;(add-hook 'window-size-change-functions #'set-appropriate-splash)
  ; TODO the window size change hook seems to crash?
  (add-hook 'doom-load-theme-hook #'set-appropriate-splash)
  )
#+end_src

Fix the random crashes hopefully caused from using the splash ðŸ˜¢
#+begin_src emacs-lisp
(defadvice! personal--load-persp-before-doom-dashboard ()
  :before #'doom-dashboard-widget-shortmenu
  (require 'persp-mode))
#+end_src

** Useful Functions
*** New Functions
Toggle the transparency of emacs
#+BEGIN_SRC emacs-lisp
;; Initialize transparency to `true`

;; TODO change this to not use global state, but instead use frame-local state

(put 'cfg-transparency 'state t)

(defun personal-toggle-transparency ()
  "Toggle the transparency of emacs"
  (interactive)
  (if (get 'cfg-transparency 'state)
      (progn
        (set-frame-parameter (selected-frame) 'alpha '(100 100))
        (put 'cfg-transparency 'state nil))
    (progn
      (set-frame-parameter (selected-frame) 'alpha '(70 70))
      (put 'cfg-transparency 'state t))
    ))
#+END_SRC

Control size of Emacs frame:
#+BEGIN_SRC emacs-lisp
(defun personal-frame-change-size (width height)
  "Modify size of window frame by increasing it by WIDTH and HEIGHT."
  (let ((cur-width (frame-width (window-frame)))
        (cur-height (frame-height (window-frame))))
    (set-frame-size (window-frame) (+ cur-width width) (+ cur-height height))))

(defun personal-frame-full-screen ()
  "Toggle frame to be fullscreen."
  (interactive)
  (toggle-frame-fullscreen))

(defun personal-frame-increase-width ()
  "Increase frame width by 1."
  (interactive)
  (personal-frame-change-size 1 0))

(defun personal-frame-decrease-width ()
    "Decrease frame width  by 1."
    (interactive)
    (personal-frame-change-size -1 0))

(defun personal-frame-increase-height ()
    "Increase frame height by 1."
    (interactive)
    (personal-frame-change-size 0 1))

(defun personal-frame-decrease-height ()
    "Decrease frame height by 1."
    (interactive)
    (personal-frame-change-size 0 -1))

(defun personal-frame-increase-diag ()
    "Increase frame width and height by 1."
    (interactive)
    (personal-frame-change-size 1 1))

(defun personal-frame-decrease-diag ()
    "Decrease frame width and height by 1."
    (interactive)
    (personal-frame-change-size -1 -1))
#+END_SRC

Control position of emacs frame:
#+BEGIN_SRC emacs-lisp
(defun personal-frame-move (down right)
  "Move window frame by DOWN and RIGHT."
  (pcase (frame-position)
    (`(,x . ,y) (set-frame-position (selected-frame) (+ x right) (+ y down)))))

(defun personal-move-frame-down-30 ()
  "Move window frame down 30."
  (interactive)
  (personal-frame-move 30 0))

(defun personal-move-frame-down-5 ()
  "Move window frame down 5."
  (interactive)
  (personal-frame-move 5 0))

(defun personal-move-frame-up-30 ()
  "Move window frame up 30."
  (interactive)
  (personal-frame-move -30 0))

(defun personal-move-frame-up-5 ()
  "Move window frame down 5."
  (interactive)
  (personal-frame-move -5 0))

(defun personal-move-frame-left-30 ()
  "Move window frame left 30."
  (interactive)
  (personal-frame-move 0 -30))

(defun personal-move-frame-left-5 ()
  "Move window frame left 5."
  (interactive)
  (personal-frame-move 0 -5))

(defun personal-move-frame-right-30 ()
  "Move window frame right 30."
  (interactive)
  (personal-frame-move 0 30))

(defun personal-move-frame-right-5 ()
  "Move window frame right 5."
  (interactive)
  (personal-frame-move 0 5))
#+END_SRC

Change fill-column
#+BEGIN_SRC elisp
#+END_SRC

Open up terminal in narrow vertical split
#+BEGIN_SRC emacs-lisp
(defun personal-sterm ()
  "Opens a terminal in a split on the left"
  (interactive)
  (require 'vterm)
  (progn
    (split-window-right 45)
    (+vterm/here t)))
#+END_SRC

Open up google in a split
#+BEGIN_SRC elisp
(defun personal-google-split ()
  "Open google in vertical split using xwidget-webkit"
  (interactive)
  (let ((google-url "https://www.google.com")
        (xwidget-buffer (lambda ()
                          (seq-find
                           (lambda (b) (eq (buffer-local-value 'major-mode b) 'xwidget-webkit-mode))
                           (buffer-list)))))
    (split-window-right)
    (xwidget-webkit-browse-url google-url)
    (switch-to-buffer (funcall xwidget-buffer))))

(defun personal-google-here ()
  "Open google in current buffer"
  (interactive)
  (let ((google-url "https://www.google.com"))
    (personal--xwidget-buffer google-url)))
#+END_SRC

Open google in window with xwidget
#+BEGIN_SRC elisp
(defun personal-open-google ()
  "Open google in in window using xwidget-webkit"
  (interactive)
  (let ((google-url "https://www.google.com"))
    (xwidget-webkit-browse-url google-url)))
#+END_SRC

Add =company-tabnine= to front of completions, which uses GPT-2 to power completions
Some functions to modify =company-backends= on the fly.
#+BEGIN_SRC elisp
(defun personal-use-tabnine-completions ()
  "Prepends tabnine to the front of company backends."
  (interactive)
  (setq company-backends (push  'company-tabnine company-backends)))


(defun personal-use-capf-and-tabnine-completions ()
  "Prepends capf and tabnine to the front of company backends."
  (interactive)
  (setq company-backends (push '(:seperate company-capf company-tabnine) company-backends)))

(defun personal-use-capf-completions ()
  "Prepends capf to the front of company backends"
  (interactive)
  (setq company-backends (push 'company-capf company-backends)))
#+END_SRC
*** Changing Old Ones
... coming soon

*** School
ssh into cornell linux box
#+BEGIN_SRC emacs-lisp
(defun personal-connect-cornell ()
  (interactive)
  (dired "/ssh:pno3@ugclinux.cs.cornell.edu:/home/pno3"))
#+END_SRC

** Keybindings and Custom Commands
*** Keybindings
Make moving around splits as easy as pressing space.
#+BEGIN_SRC emacs-lisp
(map! :leader :mode 'global
  "h" #'evil-window-left
  "l" #'evil-window-right
  "j" #'evil-window-down
  "k" #'evil-window-up

  "H" #'+evil/window-move-left
  "L" #'+evil/window-move-right
  "K" #'+evil/window-move-up
  "J" #'+evil/window-move-down
  )
#+END_SRC

Change =;= to =:= (for vim ex mode)
#+BEGIN_SRC emacs-lisp
(map! :nv
  ";" 'evil-ex
  )
#+END_SRC

Map "s" to the sneak motion
#+BEGIN_SRC emacs-lisp
;; Unbind "s" from everything else
(map!
 :map evil-normal-state-map
 "s" nil
 "S" nil)

;; Then bind it
(map!
 :nv "s" #'evil-avy-goto-char-2-below
 :n "S" #'evil-avy-goto-char-2-above)
#+END_SRC

*** Custom ex Commands
~:G~ for magit status
#+BEGIN_SRC emacs-lisp
(evil-ex-define-cmd "G" #'magit-status)
#+END_SRC

~:S~ to search google and ~:SS~ to open google in current frame
#+BEGIN_SRC emacs-lisp
(evil-ex-define-cmd "S" #'personal-google-split)
(evil-ex-define-cmd "SS" #'personal-google-here)
#+END_SRC

~:E~ to search google with eww
#+BEGIN_SRC emacs-lisp
(defun personal-eww-google ()
  (interactive)
  (let* ((query (read-from-minibuffer "Search for: "))
         (url-extension (replace-regexp-in-string " " "+" query )))
  (eww (concat "https://www.google.com/search?q=" url-extension))))

(evil-ex-define-cmd "E" #'personal-eww-google)
#+END_SRC

~:Fexplore~ to open a project drawer
#+BEGIN_SRC emacs-lisp
(evil-ex-define-cmd "Fexplore" #'treemacs)
#+END_SRC

~:PRReview~ to diff between two branches
#+BEGIN_SRC emacs-lisp
(evil-ex-define-cmd "PRReview" #'magit-diff-range)
#+END_SRC

~:STerm~ to open terminal in a split
#+BEGIN_SRC emacs-lisp
(evil-ex-define-cmd "STerm" #'personal-sterm)
#+END_SRC

** Fixing Things
A place for hacks and functions to fix fires

Fix icons when run as daemon
#+BEGIN_SRC emacs-lisp
(defun personal-set-company-box-icons ()
  (interactive)
  (setq company-box-icons-all-the-icons
        (let ((all-the-icons-scale-factor 0.8))
          `((Unknown       . ,(all-the-icons-material "find_in_page"             :face 'all-the-icons-purple))
            (Text          . ,(all-the-icons-material "text_fields"              :face 'all-the-icons-green))
            (Method        . ,(all-the-icons-material "functions"                :face 'all-the-icons-red))
            (Function      . ,(all-the-icons-material "functions"                :face 'all-the-icons-red))
            (Constructor   . ,(all-the-icons-material "functions"                :face 'all-the-icons-red))
            (Field         . ,(all-the-icons-material "functions"                :face 'all-the-icons-red))
            (Variable      . ,(all-the-icons-material "adjust"                   :face 'all-the-icons-blue))
            (Class         . ,(all-the-icons-material "class"                    :face 'all-the-icons-red))
            (Interface     . ,(all-the-icons-material "settings_input_component" :face 'all-the-icons-red))
            (Module        . ,(all-the-icons-material "view_module"              :face 'all-the-icons-red))
            (Property      . ,(all-the-icons-material "settings"                 :face 'all-the-icons-red))
            (Unit          . ,(all-the-icons-material "straighten"               :face 'all-the-icons-red))
            (Value         . ,(all-the-icons-material "filter_1"                 :face 'all-the-icons-red))
            (Enum          . ,(all-the-icons-material "plus_one"                 :face 'all-the-icons-red))
            (Keyword       . ,(all-the-icons-material "filter_center_focus"      :face 'all-the-icons-red))
            (Snippet       . ,(all-the-icons-material "short_text"               :face 'all-the-icons-red))
            (Color         . ,(all-the-icons-material "color_lens"               :face 'all-the-icons-red))
            (File          . ,(all-the-icons-material "insert_drive_file"        :face 'all-the-icons-red))
            (Reference     . ,(all-the-icons-material "collections_bookmark"     :face 'all-the-icons-red))
            (Folder        . ,(all-the-icons-material "folder"                   :face 'all-the-icons-red))
            (EnumMember    . ,(all-the-icons-material "people"                   :face 'all-the-icons-red))
            (Constant      . ,(all-the-icons-material "pause_circle_filled"      :face 'all-the-icons-red))
            (Struct        . ,(all-the-icons-material "streetview"               :face 'all-the-icons-red))
            (Event         . ,(all-the-icons-material "event"                    :face 'all-the-icons-red))
            (Operator      . ,(all-the-icons-material "control_point"            :face 'all-the-icons-red))
            (TypeParameter . ,(all-the-icons-material "class"                    :face 'all-the-icons-red))
            (Template      . ,(all-the-icons-material "short_text"               :face 'all-the-icons-green))
            (ElispFunction . ,(all-the-icons-material "functions"                :face 'all-the-icons-red))
            (ElispVariable . ,(all-the-icons-material "check_circle"             :face 'all-the-icons-blue))
            (ElispFeature  . ,(all-the-icons-material "stars"                    :face 'all-the-icons-orange))
            (ElispFace     . ,(all-the-icons-material "format_paint"             :face 'all-the-icons-pink))))))

(add-hook! 'server-after-make-frame-hook #'personal-set-company-box-icons)
#+END_SRC

** Abbrevs
Manage abbreviations
#+BEGIN_SRC emacs-lisp
(quietly-read-abbrev-file (expand-file-name "~/.doom.d/abbrev.el"))
#+END_SRC

* Modes
** Text Mode
Mode for human readable text

Recommend words when typing in text mode files:
#+BEGIN_SRC emacs-lisp
(add-hook! text-mode
  (set-company-backend! 'text-mode 'company-dabbrev 'company-ispell)
)
#+END_SRC

** Org Mode
*** Look
Set the org directory for org related files
#+BEGIN_SRC emacs-lisp
(setq org-directory "~/org/agenda/")
#+END_SRC

Make org-mode documents look a little nicer by hiding markers and showing symbols
#+BEGIN_SRC emacs-lisp
(setq org-hide-emphasis-markers t)
(setq org-pretty-entities t)
#+END_SRC

#+BEGIN_SRC emacs-lisp
(setq
    org-superstar-headline-bullets-list '("â–" "â—‰" "â—‹" "âœ¸" "âœ¿")
)
#+END_SRC

_Snippets to prettify Org mode based on this:_ [[https://zzamboni.org/post/beautifying-org-mode-in-emacs/][Beautifying Org Mode in Emacs]]:

Make org-mode files use variable pitch fonts to look more like text documents
#+BEGIN_SRC emacs-lisp
(add-hook! org-mode #'variable-pitch-mode)
(add-hook! org-mode #'personal--set-variable-pitch-faces)
#+END_SRC

Files can opt out by having this at the *end* of the file:
#+BEGIN_EXAMPLE org
;; Local Variables:
;; eval: (variable-pitch-mode 0)
;; End:
#+END_EXAMPLE

Some things shouldn't be variable pitch in org-mode ever:
#+BEGIN_SRC emacs-lisp
(after! org
  (custom-theme-set-faces
   'user
   '(org-block ((t (:inherit fixed-pitch))))
   '(org-block-begin-line ((t (:inherit fixed-pitch))))
   '(org-code ((t (:inherit (fixed-pitch)))))
   '(org-document-info-keyword ((t (:inherit (shadow fixed-pitch)))))
   '(org-indent ((t (:inherit (org-hide fixed-pitch)))))
   '(org-meta-line ((t (:inherit (font-lock-comment-face fixed-pitch)))))
   '(org-property-value ((t (:inherit fixed-pitch))) t)
   '(org-special-keyword ((t (:inherit (font-lock-comment-face fixed-pitch)))))
   '(org-table ((t (:inherit fixed-pitch))))
   '(org-drawer ((t (:inherit fixed-pitch))))
   '(org-tag ((t (:inherit (fixed-pitch) :height 0.8))))
   '(org-verbatim ((t (:inherit (shadow fixed-pitch)))))))
#+END_SRC

*** Functionality
Set up completion for org mode files
Recommend words when typing in text mode files:
#+begin_src emacs-lisp
;; (add-hook! org-mode
;;   (set-company-backend! 'org-mode 'company-dabbrev 'company-ispell))
#+end_src

Add =proselint= to lint org-mode
#+begin_src elisp
(setq flycheck-proselint-executable "~/Library/Python/3.8/bin/proselint")
#+end_src

Setup =org-download=, which makes the process of putting images into orgmode much easier
Put downloaded images into an =images= directory and include timestamp. Use the command =pngpaste= to get the image
from the clipboard.
#+begin_src emacs-lisp
(after! org-download
  (setq org-download-method 'directory)
  (setq-default org-download-image-dir "images")
  (setq org-download-timestamp "%Y%m%d-%H%M%S_")
  (setq org-download-screenshot-method "/usr/local/bin/pngpaste %s"))
#+end_src

Advice =org-fancy-priorities-mode= which sometimes errors since =org-download= hasn't loaded yet
#+begin_src emacs-lisp
(defadvice! personal--load-org-download-with-fancy-priorities ()
  :before #'org-fancy-priorities-mode
  (require 'org-download))
#+end_src

Allow pasting of images with control over the width, and bind it to keybind
#+begin_src emacs-lisp
(defun org-download-screenshot-with-size ()
  "Prompt user for a width to paste the image. Only lasts for this one function"
  (interactive)
  (let ((width (read-number "Enter width: ")) (prev-width org-download-image-html-width))
    (progn
      (setq org-download-image-html-width width)
      (org-download-screenshot)
      (setq org-download-image-html-width prev-width))))

(map! :mode 'org-mode :leader
      "v" #'org-download-screenshot-with-size)
#+end_src

Make drag-and-drop of images work with dired (untested)
#+BEGIN_SRC emacs-lisp
;; Drag-and-drop to `dired`
(add-hook 'dired-mode-hook 'org-download-enable)
#+END_SRC

Make latex previews nice and big
#+BEGIN_SRC emacs-lisp
(after! org
  (setq org-format-latex-options (plist-put org-format-latex-options :scale 3.0)))
#+END_SRC

** Org Journal
Encrypt the journal
#+BEGIN_SRC elisp
;; ...
#+END_SRC

** Ivy
Config for the search engine, Ivy.

Allow fuzzy searches to make it easier to find matches with less thought.
#+BEGIN_SRC emacs-lisp
(setq ivy-re-builders-alist
      '((t . ivy--regex-fuzzy)))
#+END_SRC

Interactive functions to make toggling fuzzy search and strict search easier
#+BEGIN_SRC emacs-lisp
(defun personal-set-fuzzy-ivy ()
  "Make ivy use fuzzy searching"
  (interactive)
  (setq ivy-re-builders-alist
        '((t . ivy--regex-fuzzy))))

(defun personal-set-strict-ivy ()
  "Make ivy use more strict searching"
  (interactive)
  (setq ivy-re-builders-alist
        '((t . ivy--regex))))
#+END_SRC

Show icons in ivy buffer
#+BEGIN_SRC emacs-lisp
(all-the-icons-ivy-rich-mode 1)
(ivy-rich-mode)
#+END_SRC

** Working with PDFs
Make PDFs look sharper on MacOS
#+BEGIN_SRC emacs-lisp
(setq pdf-view-use-scaling t pdf-view-use-imagemagick nil)
#+END_SRC

** Company
Configure completion settings;
Make it faster for code buffers, but slow for org-mode when writing prose
#+BEGIN_SRC emacs-lisp
(defun personal-set-company-for-prose ()
  "Change company configs for writing prose"
  (interactive)
  (setq
   company-idle-delay 0.3
   company-minimum-prefix-length 4))

(defun personal-set-company-for-code ()
  "Change company configs for writing code"
  (interactive)
  (setq
   company-idle-delay 0.1
   company-minimum-prefix-length 2))
#+END_SRC

** Company Box
Make the company-box frame not transparent
#+BEGIN_SRC emacs-lisp
(after! company-box
  (push '(alpha-background . 1.0) company-box-frame-parameters))
#+END_SRC

Fix an issue with =company-box= mode in Doom Emacs, where using perspectives will cause an error that will stop it
from working until Emacs is restarted
#+begin_src emacs-lisp
(defun personal-company-box-recreate-main-frame ()
  (interactive)
    (company-box--set-frame (company-box--make-frame)))

(defun personal-company-box-kill-doc-frame ()
  (interactive)
  (delete-frame (frame-local-getq company-box-doc-frame)))
#+end_src

** Flycheck
Set python path
#+BEGIN_SRC emacs-lisp
(setq flycheck-json-python-json-executable "/usr/local/bin/python3")
#+END_SRC

handle next checkers after =lsp=
#+begin_src emacs-lisp
(defvar-local personal-flycheck-local-cache nil)

(defun personal-flycheck-checker-get (fn checker property)
  (or (alist-get property (alist-get checker personal-flycheck-local-cache))
      (funcall fn checker property)))

(advice-add 'flycheck-checker-get :around 'personal-flycheck-checker-get)

(add-hook 'lsp-managed-mode-hook
          (lambda ()
            (when (derived-mode-p 'sh-mode)
              (setq personal-flycheck-local-cache '((lsp . ((next-checkers . (sh-posix-bash)))))))
            (when (derived-mode-p 'c++-mode)
              (setq personal-flycheck-local-cache '((lsp . ((next-checkers . (c/c++-cppcheck)))))))))

#+end_src
** Evil-snipe mode
Disable =evil-snipe= mode to use =evil-avy-goto-char-2-below=
#+BEGIN_SRC emacs-lisp
(remove-hook 'doom-first-input-hook #'evil-snipe-mode)
#+END_SRC

** Evil quickscope
Always turn on quick scope in programming modes
#+BEGIN_SRC elisp
(add-hook 'prog-mode-hook 'turn-on-evil-quickscope-always-mode)
#+END_SRC

Disable font for second matches, and make the letter be bold for first matches
#+BEGIN_SRC elisp
(custom-theme-set-faces
   'user
   '(evil-quickscope-first-face ((t (:inherit unspecified :weight semibold))))
   '(evil-quickscope-second-face ((t (:inherit unspecified :weight unspecified)))))
#+END_SRC

** Epaint
Disable evil mode in =epaint= mode by default, add back the space based window navigation, and the ability to kill the
buffer with "zx"
#+BEGIN_SRC elisp
(after! epaint
  (evil-set-initial-state 'epaint-mode 'emacs)
  (map! :after epaint
        :map epaint-mode-map
        " h" #'evil-window-left
        " l" #'evil-window-right
        " j" #'evil-window-down
        " k" #'evil-window-up
        " H" #'+evil/window-move-left
        " L" #'+evil/window-move-right
        " K" #'+evil/window-move-up
        " J" #'+evil/window-move-down
        "zx" #'kill-current-buffer))
#+END_SRC

** Floatbuf
    Add key mappings for floating buffers
#+BEGIN_SRC emacs-lisp
(require 'floatbuf)

(after! floatbuf
  (push '(alpha . 100) floatbuf-frame-params)
  (push '(alpha-background . 0.65) floatbuf-frame-params)
  ;; with buffer
  (defun personal-floatbuf-with-buffer ()
    "Create floating buffer with the current buffer."
    (interactive)
    (floatbuf-make-floatbuf))
  ;; with terminal
  (defun personal-floatbuf-with-terminal ()
    "Create floating buffer with the a terminal."
    (interactive)
    (floatbuf-make-floatbuf)
    (+vterm/here nil))
  ;; scratch buffer
  (defun personal-floatbuf-with-scratch ()
    "Create floating buffer with the a scratch buffer"
    (interactive)
    (floatbuf-make-floatbuf-with-buffer (get-buffer "*scratch*")))
  ;; Epaint
  (defun personal-floatbuf-with-epaint ()
    "Create floating buffer with an epaint buffer"
    (interactive)
    (floatbuf-make-floatbuf)
    (epaint))
  ;; web browser
  (defun personal-floatbuf-with-xwidget ()
    "Create floating buffer with the xwidget"
    (interactive)
    (floatbuf-make-floatbuf)
    (personal-google-here))
  ;; mapping
  (map! :leader :mode 'global
        "fbS" #'personal-floatbuf-with-xwidget
        "fbe" #'personal-floatbuf-with-epaint
        "fbf" #'floatbuf-make-floatbuf
        "fbs" #'personal-floatbuf-with-scratch
        "fbt" #'personal-floatbuf-with-terminal))
#+END_SRC

** Lsp mode
Make =lsp-ui= frame not transparent
#+BEGIN_SRC elisp
(after! lsp-ui
  (push '(alpha . 100) lsp-ui-doc-frame-parameters))
#+END_SRC

Disable =lsp-ui-doc=
#+begin_src emacs-lisp
(after! lsp-ui
  (setq lsp-ui-doc-enable nil))
#+end_src

** Centaur Tabs
Turn off tabs when the frame has a lot of splits in it (change function =personal-tab-window-limit=)
#+begin_src emacs-lisp
(defun personal-manage-tabs-with-splits (num-splits)
  "Hide tab bar when NUM-SPLITS or more (>=) windows are in the frame."
  (if (length> (window-list) (- num-splits 1))
    (walk-windows (lambda (w) (with-selected-window w (centaur-tabs-local-mode 1))))
    (walk-windows (lambda (w) (with-selected-window w (centaur-tabs-local-mode -1))))))

(defun personal-tab-window-limit ()
  "Function that is used in hook that calls `personal-manage-tabs-with-splits' with arg."
  (unless (frame-parent) ;; don't mess with tabs if its a child frame
    (personal-manage-tabs-with-splits 3)))

(after! centaur-tabs
  (add-hook! 'window-configuration-change-hook #'personal-tab-window-limit))
#+end_src

** Elfeed
Set the org file that configures feed
#+begin_src emacs-lisp
(setq rmh-elfeed-org-files '("~/.doom.d/elfeed.org"))
#+end_src

** vterm
Bind key to =vterm-other-window= which happens to open a terminal to the directory of the file it was
invoked in (useful)
#+begin_src emacs-lisp
(defun personal-vterm-here ()
  (interactive)
  "Open vterm in the directory of cursor.
Just calls `vterm-other-window', but also requires it to make it load beforehand."
  (require 'vterm)
  (vterm-other-window))

(map! :leader :mode 'global
      "oh" #'personal-vterm-here)
#+end_src
** vlc
From [[https://ag91.github.io/blog/2021/01/25/vlc-via-emacs-how-to-open-a-youtube-link/][this page]]; setting up VLC so I can browse music in Emacs
#+begin_src emacs-lisp
(after! vlc
  (defun vlc-add-uri (uri &optional noaudio novideo)
    "Add URI to playlist and start playback.
NOAUDIO and NOVIDEO are optional options.
If NOAUDIO is non-nil, disable audio.
If NOVIDEO is non-nil, disable video.
When called interactively, with prefix arg, you can pick one."
    (interactive (cons (let ((uri (read-string "Add file or url: ")))
                         (if (s-starts-with-p "http" uri) uri
                           (concat "file://" (expand-file-name uri))))
                       (pcase current-prefix-arg
                         ('nil (list nil nil))
                         (_ (pcase (completing-read "Option: " '("noaudio" "novideo") nil t)
                              ("noaudio" (list t nil))
                              ("novideo" (list nil t)))))))
    (vlc-add uri noaudio novideo))

  (defun vlc-enqueue-uri (uri)
    "Add URI to playlist."
    (interactive (list (let ((uri (read-string "Add file or url: ")))
                         (if (s-starts-with-p "http" uri) uri
                           (concat "file://" (expand-file-name uri))))
                       ))
    (vlc-enqueue uri)))
#+end_src

Suggested from above link: add videos to queue if cursor is on it, and link in kill ring
#+begin_src emacs-lisp
(defun vlc-enqueue-uri-at-point ()
    "Add URI to playlist."
    (interactive)
    (let ((uri (thing-at-point 'url)))
      (when uri (vlc-enqueue uri))))

(defun vlc-uris-in-clipboard ()
    (--> (with-temp-buffer
           (clipboard-yank)
           (buffer-substring-no-properties (point-min) (point-max)))
         (s-split "\n" it)
         (--filter (s-starts-with-p "http" it) it)))

(defun vlc-enqueue-uris (uris)
    "Queue URIS to current VLC playlist."
    (interactive)
    (let ((uris (or uris (vlc-uris-in-clipboard))))
      (-each uris 'vlc-enqueue-uri)))

#+end_src

Keybinds for music playing (Use =<leader>1v= as the global keybind)
#+begin_src emacs-lisp
(map! :leader :mode 'global
      "1vs" #'vlc-start
      "1vr" #'vlc-play
      "1vR" #'vlc-pause
      "1vn" #'vlc-next
      "1vp" #'vlc-prev
      "1va" #'vlc-enqueue-uri-at-point
      "1ve" #'vlc-empty)
#+end_src

Function for opening up list
#+begin_src emacs-lisp
(defun personal-vlc-music-list ()
  (interactive)
  (let ((buf (find-file-noselect "~/Dropbox/agenda/music.org")))
    (floatbuf-make-floatbuf-with-buffer buf)))

(map! :leader :mode 'global
      "fbv" #'personal-vlc-music-list)
#+end_src

Strip music org file of song urls
#+begin_src emacs-lisp
(defun personal-queue-all-into-vlc ()
  "Use awk to put all files into vlc queue"
  (interactive)
  (require 'vlc)
  (let*
      ((music-file-loc (expand-file-name "~/Dropbox/agenda/music.org"))
       (awk-script-loc (expand-file-name "~/.doom.d/music-list-parse-all.awk"))
       (urls
        (shell-command-to-string (format "%s %s" awk-script-loc music-file-loc))))
    (mapc #'vlc-add-uri (split-string urls))))
#+end_src

** Tree Sitter
#+begin_src emacs-lisp
(add-hook 'c-mode-common-hook #'tree-sitter-hl-mode)
#+end_src

#+begin_src emacs-lisp
(add-hook 'vterm-mode-hook
          (lambda ()
            (set (make-local-variable 'buffer-face-mode-face) 'fixed-pitch)
                 (buffer-face-mode t)))
#+end_src
* Programming
** C Family
Setup LSP-mode with clangd based on [[file:~/.emacs.d/modules/lang/cc/README.org::*Eglot with clangd][this]]
#+BEGIN_SRC elisp
;; (set-eglot-client! 'cc-mode '("clangd" "-j=3" "--clang-tidy"))

(setq lsp-clients-clangd-args '("-j=3"
                                "--background-index"
                                "--clang-tidy"
                                "--completion-style=detailed"
                                "--header-insertion=never"))
(after! lsp-clangd (set-lsp-priority! 'clangd 2))
#+END_SRC

Map <Space>ff to =ff-find-other-file= for Cpp
#+BEGIN_SRC emacs-lisp
(map! :mode 'cpp-mode :leader
      " ff" #'ff-find-other-file)
#+END_SRC

Org source blocks should use up to date cpp
#+begin_src emacs-lisp
(defvar org-babel-default-header-args:cpp '((:flags . "-std=c++20")))
#+end_src

(default) Formatter options for the C family
#+begin_src emacs-lisp
(set-formatter! 'clang-format
  '("clang-format"
    "-style={BasedOnStyle: llvm, IndentWidth: 2, SortIncludes: true, ColumnLimit: 100}"
    ("-assume-filename=%S" (or buffer-file-name mode-result "")))
  :modes '(cpp-mode c-mode))
#+end_src

*Note to self*
Can set the =C++= version of projects using directory local variables for clang flychecker
#+begin_quote
Open the root directory of your project in Dired with C-x d, and then type M-x add-dir-local-variable RET c++-mode RET flycheck-gcc-language-standard RET "c++11". This will create a .dir-locals.el file in the root directory of your project. Emacs reads this file whenever you visit a file from this directory or any subdirectory, and sets variables according to the rules in this file. Specifically, Emacs will now set the language standard for Flycheck syntax checking to C++ 11 for all C++ files in your project.
#+end_quote

** OR Tools
Useful stuff for working with linear programs and the like.

Hook AMPL relate files to [[https://github.com/dpo/ampl-mode][ampl-mode]].
#+BEGIN_SRC emacs-lisp
(setq auto-mode-alist
      (cons '("\\.mod$" . ampl-mode) auto-mode-alist))
(setq auto-mode-alist
      (cons '("\\.dat$" . ampl-mode) auto-mode-alist))
(setq auto-mode-alist
      (cons '("\\.ampl$" . ampl-mode) auto-mode-alist))
(setq auto-mode-alist
      (cons '("\\.ampl$" . ampl-mode) auto-mode-alist))
(setq interpreter-mode-alist
      (cons '("ampl" . ampl-mode)
            interpreter-mode-alist))
(autoload 'ampl-mode "ampl-mode" "Ampl editing mode." t)
#+END_SRC

Recommend previously used words in AMPL mode
#+BEGIN_SRC emacs-lisp
(add-hook! ampl-mode
  (set-company-backend! 'ampl-mode 'company-dabbrev))
#+END_SRC
** Rust
Configs for coding in the rust programming language.

Set ~RUST_SRC_PATH~ for racer:
#+BEGIN_SRC emacs-lisp
(let* ((rust-lib-path "/.rustup/toolchains/stable-x86_64-apple-darwin/lib/rustlib/src/rust/library")
       (rust-full-path (concat (substitute-in-file-name "$HOME") rust-lib-path)))
  (setq racer-rust-src-path rust-full-path))
#+END_SRC

Setup completion in rust
#+BEGIN_SRC emacs-lisp
(add-hook! rust-mode
  (set-company-backend! 'rust-mode '(company-capf company-dabbrev company-yasnippet)))
#+END_SRC

** Swift
Configurations for the swift programming language.

Setup =lsp-sourcekit= to work swift files...
#+BEGIN_SRC emacs-lisp
;; (after! lsp-sourcekit
;;   (setenv "SOURCEKIT_TOOLCHAIN_PATH" "/Applications/Xcode.app/Contents/Developer/Toolchains/XcodeDefault.xctoolchain")
;;   (setq lsp-sourcekit-executable (string-trim (shell-command-to-string "xcrun --find sourcekit-lsp"))))
#+END_SRC

Recommend previously used words in swift files to compensate for spotty sourcekit support:
#+BEGIN_SRC emacs-lisp
(add-hook! swift-mode
  (set-company-backend! 'swift-mode '(company-sourcekit company-dabbrev company-capf company-yasnippet)))
#+END_SRC

Add =swiftlint= as a linter in swift files
#+BEGIN_SRC elisp
(with-eval-after-load 'flycheck
  (flycheck-swiftlint-setup))
#+END_SRC

** Python
#+BEGIN_SRC emacs-lisp
(setq flycheck-python-flake8-executable "~/Library/Python/3.8/bin/flake8")
#+END_SRC

Which python is used:
#+BEGIN_SRC emacs-lisp
(setq python-shell-exec-path '("/usr/local/bin/python3"))
(setq python-shell-remote-exec-path '("/usr/local/bin/python3"))
#+END_SRC

Map the commands for sending lines to REPL to something less painful
#+BEGIN_SRC emacs-lisp
(map! :mode 'python-mode :leader
      "ml" #'python-shell-send-statement
      "mr" #'python-shell-send-region
      "mf" #'python-shell-send-defun)
#+END_SRC

Stop the warning about native read line
#+begin_src emacs-lisp
(with-eval-after-load 'python
  (defun python-shell-completion-native-try ()
    "Return non-nil if can trigger native completion."
    (let ((python-shell-completion-native-enable t)
          (python-shell-completion-native-output-timeout
           python-shell-completion-native-try-output-timeout))
      (python-shell-completion-native-get-completions
       (get-buffer-process (current-buffer))
       nil "_"))))

(setq python-shell-completion-native-enable nil)
#+end_src

Set tab size and stuff
#+begin_src emacs-lisp
(add-hook 'python-mode-hook
      (lambda ()
        (setq indent-tabs-mode nil)
        (setq tab-width 4)
        (setq python-indent-offset 4)))
#+end_src

* Misc/Random
** Fun Stuff
 Fun Emacs things

 Zone mode screen saver after a good amount of idle time
 #+BEGIN_SRC emacs-lisp
 (require 'zone)
 (zone-when-idle 600) ;; 10 minutes
 #+END_SRC

Disable all non productive stuff (for lectures or presentations)
#+BEGIN_SRC emacs-lisp
(defun personal-look-professional ()
  "Disable all idle animations and related."
  (interactive)
  (huecycle-stop-idle)
  (huecycle-reset-all-faces-on-all-buffers)
  (zone-leave-me-alone))
#+END_SRC

Add rainbow to zone programs
#+BEGIN_SRC elisp
(setq zone-programs (vconcat [zone-rainbow] zone-programs))
#+END_SRC

Enable sound support
#+BEGIN_SRC elisp
(unless (and (fboundp 'play-sound-internal)
             (subrp (symbol-function 'play-sound-internal)))
  (require 'play-sound))
#+END_SRC

Set some faces to change color when idle
#+BEGIN_SRC elisp
(require 'huecycle)

(after! (huecycle doom-modeline hl-line company-box)
  (huecycle-set-faces
   ;; rainbow stuff
   ((background . hl-line)
    :random-color-hue-range (0.0 1.0)
    :random-color-saturation-range (0.1 0.45)
    :random-color-luminance-range (0.1 0.25)
    :speed 0.5
    :persist t)
   ((foreground . (doom-modeline-evil-normal-state doom-modeline-evil-insert-state
                                                   doom-modeline-buffer-major-mode
                                                   line-number-current-line doom-modeline-lsp-success doom-modeline-panel doom-modeline-info))
    :random-color-hue-range (0.0 1.0)
    :random-color-saturation-range (0.8 1.0)
    :random-color-luminance-range (0.5 0.8))
   :persist t
   ((foreground . region)
    :random-color-hue-range (0.0 1.0)
    :random-color-saturation-range (0.9 1.0)
    :random-color-luminance-range (0.5 0.8))
   ((background . company-tooltip-selection)
    :random-color-hue-range (0.0 1.0)
    :random-color-saturation-range (0.1 0.5)
    :random-color-luminance-range (0.1 0.25)))
  (setq huecycle-buffers-to-huecycle-in
        (list
         #'current-buffer
         (lambda ()
           (company-box--get-buffer))))
  (huecycle-when-idle 1.0))
#+END_SRC

** Scratch Space
Space for random elisp
#+BEGIN_SRC elisp
;; (require 'tetris-sounds)

#+END_SRC

* Orgmode footer args
#+localWords: MacOS, PDFs, ampl, Config, Configs, sourcekit, flycheck
